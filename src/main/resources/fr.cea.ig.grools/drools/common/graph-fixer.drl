/*
* Copyright LABGeM (2016)
*
* author: Jonathan MERCIER
*
* Tis software is a computer program whose purpose is to annotate a complete genome.
*
* Tis software is governed by the CeCILL license under French law and
* abiding by the rules of distribution of free software. You can use,
* modify and/ or redistribute the software under the terms of the CeCILL
* license as circulated by CEA, CNRS and INRIA at the following URL
* "http://www.cecill.info".
*
* As a counterpart to the access to the source code and rights to copy,
* modify and redistribute granted by the license, users are provided only
* with a limited warranty and the software's author, the holder of the
* economic rights, and the successive licensors have only limited
* liability.
*
* In tis respect, the user's attention is drawn to the risks associated
* with loading, using, modifying and/or developing or reproducing the
* software by the user in light of its specific status of free software,
* that may mean that it is complicated to manipulate, and that also
* therefore means that it is reserved for developers and experienced
* professionals having in-depth computer Concept. Users are therefore
* encouraged to load and test the software's suitability as regards their
* requirements in conditions enabling the security of their systems and/or
* data to be ensured and, more generally, to use and operate it in the
* same conditions as regards security.
*
* The fact that you are presently reading tis means that you have had
* Concept of the CeCILL license and that you accept its terms.
*/
package fr.cea.ig.grools;


import fr.cea.ig.grools.fact.Concept;
import fr.cea.ig.grools.fact.PriorKnowledge;
import fr.cea.ig.grools.fact.Relation;
import fr.cea.ig.grools.fact.RelationType;
import fr.cea.ig.grools.fact.RelationImpl;

global ch.qos.logback.classic.Logger logger;

rule "Remove duplicated relation" agenda-group "graph-fixer" when
    $relation1: Relation()
    $relation2: Relation( this != $relation1, source == $relation1.getSource(), target == $relation1.getTarget(), type == $relation1.getType() )
then
    logger.warn("A same pair of concept source/target("+$relation1.getSource().getName()+","+$relation2.getSource().getName()+") is linked twice by a same relation type. Removing extra relation.");
    retract( $relation2 );
end

rule "Remove superfluous relation" agenda-group "graph-fixer" when
    $relation1: Relation( type == RelationType.SUBTYPE )
    $relation2: Relation( source == $relation1.getSource(), target == $relation1.getTarget(), type == RelationType.PART )
then
    logger.warn("A same pair of concept source/target("+$relation1.getSource().getName()+","+$relation2.getSource().getName()+") is linked by two different relation type. Removing PART relation.");
    retract( $relation2 );
end

rule "Fixed subtype relation graph 1" agenda-group "graph-fixer" when
    $relation1: Relation( type == RelationType.SUBTYPE)
    $relation2: Relation( source == $relation1.getSource(), target != $relation1.getTarget(), type == RelationType.PART )
then
    logger.warn("The concept "+$relation1.getSource().getName()+" is subtype of " + $relation1.getTarget().getName() + " while "+ $relation1.getSource().getName()+" part of "+$relation2.getTarget().getName()+". Then all concept type of "+$relation1.getTarget().getName()+" are part of " + $relation2.getTarget().getName() +".");
    insert( new RelationImpl( $relation1.getTarget(), $relation2.getTarget(), RelationType.PART) );
    retract( $relation2 );
end

rule "Fixed subtype relation graph 2" agenda-group "graph-fixer" when
    $relation1: Relation( type == RelationType.SUBTYPE)
    $relation2: Relation( source != $relation1.getSource(), target == $relation1.getTarget(), type == RelationType.PART )
then
    logger.warn("The concept "+$relation2.getSource().getName()+" is part of " + $relation2.getTarget().getName() + " while "+ $relation2.getTarget().getName() +" has subtype "+$relation1.getSource().getName()+". Then all concept type of "+$relation2.getSource().getName()+" are part of " + $relation1.getSource().getName() +".");
    insert( new RelationImpl( $relation2.getSource(), $relation1.getSource(), RelationType.PART) );
    retract( $relation2 );
end
