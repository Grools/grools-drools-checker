/*
* Copyright LABGeM (2014)
*
* author: Jonathan MERCIER
*
* Tis software is a computer program whose purpose is to annotate a complete genome.
*
* Tis software is governed by the CeCILL license under French law and
* abiding by the rules of distribution of free software. You can use,
* modify and/ or redistribute the software under the terms of the CeCILL
* license as circulated by CEA, CNRS and INRIA at the following URL
* "http://www.cecill.info".
*
* As a counterpart to the access to the source code and rights to copy,
* modify and redistribute granted by the license, users are provided only
* with a limited warranty and the software's author, the holder of the
* economic rights, and the successive licensors have only limited
* liability.
*
* In tis respect, the user's attention is drawn to the risks associated
* with loading, using, modifying and/or developing or reproducing the
* software by the user in light of its specific status of free software,
* that may mean that it is complicated to manipulate, and that also
* therefore means that it is reserved for developers and experienced
* professionals having in-depth computer Concept. Users are therefore
* encouraged to load and test the software's suitability as regards their
* requirements in conditions enabling the security of their systems and/or
* data to be ensured and, more generally, to use and operate it in the
* same conditions as regards security.
*
* The fact that you are presently reading tis means that you have had
* Concept of the CeCILL license and that you accept its terms.
*/
package fr.cea.ig.grools;
import fr.cea.ig.grools.fact.Concept;
import fr.cea.ig.grools.fact.Observation;
import fr.cea.ig.grools.fact.ObservationType;
import fr.cea.ig.grools.fact.PriorKnowledge;
import fr.cea.ig.grools.fact.Relation;
import fr.cea.ig.grools.fact.RelationType;
import fr.cea.ig.grools.fact.RelationImpl;
import fr.cea.ig.grools.logic.Qualifier;
import fr.cea.ig.grools.logic.TruthValue;
import fr.cea.ig.grools.logic.TruthValueSet;
import fr.cea.ig.grools.logic.TruthValuePowerSet;

import java.util.Set;
import java.util.HashSet;
import java.util.EnumSet;

global ch.qos.logback.classic.Logger logger;

dialect "java"


rule "Prior-knowledge has a group of predictable part with any specific predicted [prediction]" no-loop true agenda-group "prediction inference" when
    // Select any prior-knowledge
    $pk: PriorKnowledge()
    // take the prior-knowledge list, where each concept is a child of $pk and linked by a the relation part-of
    $children: Set( size > 0 ) from accumulate(   $rel: Relation(   source isA PriorKnowledge,
                                                                    target == $pk,
                                                                    type   == RelationType.PART ),
                                                    init  ( Set set =  new HashSet();  ),
                                                    action( set.add( $rel.getSource() );  ),
                                                    result( set )
                                                 )

    // all children prediction is computed
    forall( PriorKnowledge( prediction != TruthValuePowerSet.n ) from $children )

    // Any concept from children is specific ( this need to be always true in normal mode )
    not( PriorKnowledge( isSpecific == true, prediction == TruthValuePowerSet.T ) from $children )

    // Merge all prediction from children together (example: {{N}} merged with {{T}} give {{N},{T}})
    $predictionFromChildren: TruthValuePowerSet(  ) from accumulate( $child: PriorKnowledge( ) from $children,
                                                                                                init  ( EnumSet truthfulness = EnumSet.noneOf( TruthValuePowerSet.class );  ),
                                                                                                action( truthfulness.add( $child.getPrediction() ); ),
                                                                                                result( TruthValuePowerSet.merge( truthfulness ) )
                                                                                              )
    // Select all predictions linked to $pk
    $obsToPk    : Set( ) from collect( Relation(    source isA Observation,
                                                    target == $pk,
                                                    type ( == ObservationType.COMPUTATION || == ObservationType.CURATION ) ) )
    $obsSet : Set() from accumulate( $rel: Relation(  ) from $obsToPk,
                                        init  ( Set set =  new HashSet(); ),
                                        action( set.add( $rel.getSource() ); ),
                                        result( set ) )
    $directPrediction: TruthValueSet(  ) from accumulate(   $obs: Observation( ) from $obsSet ,
                                                                init  ( EnumSet set =  EnumSet.noneOf(TruthValue.class);  ),
                                                                action( set.add( $obs.getTruthValue() ); ),
                                                                result( TruthValue.union( set ) ) )
    // Add the direct prediction to merged set of all prediction from children ( example: {{T}} add {N} give {{T}})
    $result: TruthValuePowerSet( this != $pk.getPrediction(), this == TruthValuePowerSet.add( $predictionFromChildren, $directPrediction ) )

    $pk_parents: Set( ) from accumulate(   $rel: Relation(  target isA PriorKnowledge,
                                                            source == $pk ),
                                            init  ( Set set =  new HashSet();  ),
                                            action( set.add(  $rel.getTarget() ); ),
                                            result( set )
                                         )
then
    logger.debug( $pk.getName()+" has part group of prior-knowledge their prediction are merged " +$predictionFromChildren+ " and we add direct observation " + $directPrediction + " to infer "+$result);
    modify( $pk ){
        setPrediction( $result )
    };
    for( final Object obj : $pk_parents){
        final PriorKnowledge parent = (PriorKnowledge)obj;
        update(parent);
    }
     kcontext.getKnowledgeRuntime().getAgenda().getAgendaGroup("qualifier mark").setFocus();
end


rule "Prior-knowledge has a group of predictable part with at least one specific and predicted [prediction]" no-loop true agenda-group "prediction inference" when
    // Select any prior-knowledge
    $pk: PriorKnowledge()
    // take the prior-knowledge list, where each concept is a child of $pk and linked by a the relation part-of
    $children: Set( size > 0 ) from accumulate(   $rel: Relation(   source isA PriorKnowledge,
                                                                    target == $pk,
                                                                    type   == RelationType.PART ),
                                                    init  ( Set set =  new HashSet();  ),
                                                    action( set.add( $rel.getSource() ); ),
                                                    result( set )
                                               )

    // all children prediction is computed
    forall( PriorKnowledge( prediction != TruthValuePowerSet.n ) from $children )

    // At least one prior-knowledge from children is specific and predicted ( this need to be always false in normal mode )
    exists( PriorKnowledge( isSpecific == true, prediction == TruthValuePowerSet.T ) from $children )


    // Merge all prediction from children together which have his prediction known (example: {{N}} is filtered so {{N}} with {{T}} give {{T}})
    $predictionFromChildren: TruthValuePowerSet( ) from accumulate(  $child: PriorKnowledge( ) from $children,
                                                                                                    init  ( EnumSet truthfulness = EnumSet.noneOf( TruthValuePowerSet.class );  ),
                                                                                                    action( if( $child.getPrediction() != TruthValuePowerSet.N && $child.getPrediction() != TruthValuePowerSet.NT ) truthfulness.add( $child.getPrediction() ); ),
                                                                                                    result( TruthValuePowerSet.merge( truthfulness ) )
                                                                                                 )
    // Select all predictions linked to $pk
    $obsToPk    : Set( ) from collect( Relation(    source isA Observation,
                                                    target == $pk,
                                                    type ( == ObservationType.COMPUTATION || == ObservationType.CURATION ) ) )
    $obsSet : Set() from accumulate( $rel: Relation(  ) from $obsToPk,
                                        init  ( Set set =  new HashSet(); ),
                                        action( set.add( $rel.getSource() ); ),
                                        result( set ) )
    $directPrediction: TruthValueSet(  ) from accumulate(   $obs: Observation( ) from $obsSet ,
                                                                init  ( EnumSet set =  EnumSet.noneOf(TruthValue.class);  ),
                                                                action( set.add( $obs.getTruthValue() ); ),
                                                                result( TruthValue.union( set ) ) )
    // Add the direct prediction to merged set of all prediction from children ( example: {{T}} add {N} give {{T}})
    $result: TruthValuePowerSet( this != $pk.getPrediction(), this == TruthValuePowerSet.add( $predictionFromChildren, $directPrediction ) )

    $pk_parents: Set( ) from accumulate(   $rel: Relation(  target isA PriorKnowledge,
                                                            source == $pk ),
                                            init  ( Set set =  new HashSet();  ),
                                            action( set.add(  $rel.getTarget() ); ),
                                            result( set )
                                         )
then
    logger.debug( $pk.getName()+" has part group of prior-knowledge with one this group to be specific and predicted, their prediction are merged " +$predictionFromChildren+ " and we add direct observation " + $directPrediction + " to infer "+$result);
    modify( $pk ){
        setPrediction( $result )
    };
    for( final Object obj : $pk_parents){
        final PriorKnowledge parent = (PriorKnowledge)obj;
        update(parent);
    }
     kcontext.getKnowledgeRuntime().getAgenda().getAgendaGroup("qualifier mark").setFocus();
end


rule "Prior-knowledge has subtype prior-knowledge [prediction]" no-loop true agenda-group "prediction inference" when
    // Select any prior-knowledge
    $parent: PriorKnowledge(  )
    // take the prior-knowledge list, where each concept is a child of $pk and linked by a the relation has-subtype
    $relation: Relation(     source     isA         PriorKnowledge,
                            target      ==          $parent,
                            qualifiers  contains    Qualifier.GREATEST_TRUTH_DEGREE,
                            type        ==          RelationType.SUBTYPE  )
    $child: PriorKnowledge( this == $relation.getSource( ) )
    // Select all predictions linked to $pk
    $obsToPk    : Set( ) from collect( Relation(    source isA Observation,
                                                    target == $parent,
                                                    type ( == ObservationType.COMPUTATION || == ObservationType.CURATION ) ) )
    $obsSet : Set() from accumulate( $rel: Relation(  ) from $obsToPk,
                                        init  ( Set set =  new HashSet(); ),
                                        action( set.add( $rel.getSource() ); ),
                                        result( set ) )
    $directPrediction: TruthValueSet(  ) from accumulate(   $obs: Observation( ) from $obsSet ,
                                                                init  ( EnumSet set =  EnumSet.noneOf(TruthValue.class);  ),
                                                                action( set.add( $obs.getTruthValue() ); ),
                                                                result( TruthValue.union( set ) ) )
    // Add the direct expectation to merged set of all prediction from parents ( example: {{T}} add {N} give {{T}})
    $result: TruthValuePowerSet( this != $parent.getPrediction(), this == TruthValuePowerSet.add( $child.getPrediction(), $directPrediction ) )

    $pk_parents: Set( ) from accumulate(   $rel: Relation(  target isA PriorKnowledge, source == $parent ),
                                            init  ( Set set =  new HashSet();  ),
                                            action( set.add(  $rel.getTarget() );),
                                            result( set )
                                         )
then
    logger.debug( $parent.getName()+" has subtype group of prior-knowledge, their greatest prediction are keep " +$child.getPrediction()+ " and we add direct observation " + $directPrediction + " to infer "+$result);
    modify( $parent ){
        setPrediction( $result )
    };
    for( final Object obj : $pk_parents){
        final PriorKnowledge parent = (PriorKnowledge)obj;
        update(parent);
    }
     kcontext.getKnowledgeRuntime().getAgenda().getAgendaGroup("qualifier mark").setFocus();
end



rule "Prior-knowledge is part of another prior-knowledge [expectation]" no-loop true agenda-group "expectation inference" when
    // Select any prior-knowledge
    $pk: PriorKnowledge( prediction != TruthValuePowerSet.n )
    // take the prior-knowledge list, where each concept is a parent of $pk and linked by a the relation part-of
    $parents: TruthValuePowerSet( size > 0 ) from accumulate(   $rel: Relation(   target  isA PriorKnowledge,
                                                                        source  ==  $pk,
                                                                        type    == RelationType.PART ),
                                                    init  ( Set set =  new HashSet();  ),
                                                    action( set.add( $rel.getTarget() ); ),
                                                    result( set )
                                                 )
    // all parents expectation is computed
    forall( PriorKnowledge( expectation != TruthValuePowerSet.n ) from $parents )

    // Merge all expectation from parents together (example: {{N}} merged with {{T}} give {{N},{T}})
    $expectationFromParents: TruthValuePowerSet( ) from accumulate(   $parent: PriorKnowledge( ) from $parents,
                                                                                                    init  ( EnumSet truthfulness = EnumSet.noneOf( TruthValuePowerSet.class );  ),
                                                                                                    action( truthfulness.add( $parent.getExpectation() ); ),
                                                                                                    result( TruthValuePowerSet.merge( truthfulness ) )
                                                                                                 )
    // Select all expectations linked to $pk
    $obsToPk : Set( ) from collect( Relation(   source isA Observation,
                                                target == $pk,
                                                type ( == ObservationType.EXPERIMENTATION || == ObservationType.CURATION ) ) )
    $obsSet : Set() from accumulate( $rel: Relation(  ) from $obsToPk,
                                            init  ( Set set =  new HashSet(); ),
                                            action( set.add( $rel.getSource() ); ),
                                            result( set ) )
    $directExpectation: TruthValueSet(  ) from accumulate(   $obs: Observation( ) from $obsSet ,
                                                                init  ( EnumSet set =  EnumSet.noneOf(TruthValue.class);  ),
                                                                action( set.add( $obs.getTruthValue() ); ),
                                                                result( TruthValue.union( set ) ) )
    // Add the direct expectation to merged set of all prediction from parents ( example: {{T}} add {N} give {{T}})
    $result: TruthValuePowerSet( this != $pk.getExpectation(), this == TruthValuePowerSet.add( $expectationFromParents, $directExpectation ) )
    $pk_children: Set( ) from accumulate(  $rel: Relation(  source isA PriorKnowledge.class, target == $pk ),
                                            init  ( Set set =  new HashSet();  ),
                                            action( set.add(  $rel.getSource() ); ),
                                            result( set ) )
then
    logger.debug( $pk.getName()+" is part of one or more parents prior-knowledge their expectation are merged " +$expectationFromParents+ " and we add direct observation " + $directExpectation + " to infer "+$result);
    modify( $pk ){
        setExpectation( $result )
    };
    for( final Object obj : $pk_children){
        final PriorKnowledge child = (PriorKnowledge)obj;
        update(child);
    }
     kcontext.getKnowledgeRuntime().getAgenda().getAgendaGroup("conclusion").setFocus();
     kcontext.getKnowledgeRuntime().getAgenda().getAgendaGroup("prediction inference").setFocus();
end


rule "Prior-knowledge is subtype of another prior-knowledge and has one of the greatest prediction truth value [expectation]" no-loop true agenda-group "expectation inference" when
    // Select any prior-knowledge
    $pk: PriorKnowledge( prediction != TruthValuePowerSet.n )
    // $pk has the one of greatest truth value degree from his prediction compared to all others children
    $relation: Relation(    target      isA PriorKnowledge,
                            source      ==  $pk,
                            qualifiers  contains  Qualifier.GREATEST_TRUTH_DEGREE,
                            type        ==  RelationType.SUBTYPE )
    // Select all parents of $pk
    $parents: Set( ) from accumulate(    $rel: Relation(  target isA PriorKnowledge, source == $pk, type == RelationType.SUBTYPE ),
                                                    init  ( Set set =  new HashSet();  ),
                                                    action( set.add(  $rel.getTarget() ); ),
                                                    result( set ) )
    // Parent expectation have not a false truth value
    forall( PriorKnowledge( expectation ( != TruthValuePowerSet.F && != TruthValuePowerSet.n ) ) from $parents )

     // Merge all expectation from parents together (example: {{N}} merged with {{T}} give {{N},{T}})
    $expectationFromParents: TruthValuePowerSet( ) from accumulate( $parent: PriorKnowledge( ) from $parents,
                                                                    init  ( EnumSet truthfulness = EnumSet.noneOf( TruthValuePowerSet.class );  ),
                                                                    action( truthfulness.add( $parent.getExpectation() ); ),
                                                                    result( TruthValuePowerSet.merge( truthfulness ) ) )
    // Select all expectations linked to $pk
    $obsToPk : Set( ) from collect( Relation(  source isA Observation,
                                                        target == $pk,
                                                        type ( == ObservationType.EXPERIMENTATION || == ObservationType.CURATION ) ) )
    $obsSet : Set() from accumulate( $rel: Relation(  ) from $obsToPk,
                                            init  ( Set set =  new HashSet(); ),
                                            action( set.add( $rel.getSource() ); ),
                                            result( set ) )
    $directExpectation: TruthValueSet(  ) from accumulate(   $obs: Observation( ) from $obsSet ,
                                                                init  ( EnumSet set =  EnumSet.noneOf(TruthValue.class);  ),
                                                                action( set.add( $obs.getTruthValue() ); ),
                                                                result( TruthValue.union( set ) ) )

    // Add the direct expectation to merged set of all prediction from parents ( example: {{T}} add {N} give {{T}})
    $result: TruthValuePowerSet( this != $pk.getExpectation(), this == TruthValuePowerSet.add( $expectationFromParents, $directExpectation ) )

    $pk_children: Set( ) from accumulate(  $rel: Relation(  source isA PriorKnowledge, target == $pk ),
                                            init  ( Set set =  new HashSet();  ),
                                            action( set.add(  $rel.getSource() ); ),
                                            result( set ) )
then
    logger.debug( $pk.getName()+" is subtype of one or more parents prior-knowledge and has the greatest prediction with at least one parent their expectation are merged " +$expectationFromParents+ " and we add direct observation " + $directExpectation + " to infer "+$result);
    modify( $pk ){
        setExpectation( $result )
    };
    for( final Object obj : $pk_children){
        final PriorKnowledge child = (PriorKnowledge)obj;
        update(child);
    }
     kcontext.getKnowledgeRuntime().getAgenda().getAgendaGroup("conclusion").setFocus();
     kcontext.getKnowledgeRuntime().getAgenda().getAgendaGroup("prediction inference").setFocus();
end


rule "Prior-knowledge is subtype of another prior-knowledge and is not one of the greatest prediction truth value [expectation]" no-loop true agenda-group "expectation inference" when
    // Select any prior-knowledge
    $pk: PriorKnowledge( prediction != TruthValuePowerSet.n )
    not( Relation(   target      isA PriorKnowledge,
                     source      ==  $pk,
                     qualifiers  contains  Qualifier.GREATEST_TRUTH_DEGREE,
                     type        ==  RelationType.SUBTYPE ))
    // $pk has the one of greatest truth value degree from his prediction compared to all others children
    $relation: Relation(    target      isA PriorKnowledge,
                            source      ==  $pk,
                            qualifiers  not contains  Qualifier.GREATEST_TRUTH_DEGREE,
                            type        ==  RelationType.SUBTYPE )
    // Select all parents of $pk
    $parents: Set( ) from accumulate(    $rel: Relation(  target isA PriorKnowledge, source == $pk, type == RelationType.SUBTYPE ),
                                                    init  ( Set set =  new HashSet();  ),
                                                    action( set.add(  $rel.getTarget() ); ),
                                                    result( set ) )
    // Parent expectation have not a false truth value
    forall( PriorKnowledge( expectation ( != TruthValuePowerSet.F && != TruthValuePowerSet.n ) ) from $parents )

    // Merge all expectation from parents together (example: {{N}} merged with {{T}} give {{N},{T}})
    $expectationFromParents: TruthValuePowerSet( ) from accumulate( $parent: PriorKnowledge( ) from $parents,
                                                                    init  ( EnumSet truthfulness = EnumSet.noneOf( TruthValuePowerSet.class );  ),
                                                                    action( truthfulness.add( $parent.getExpectation() ); ),
                                                                    result( TruthValuePowerSet.merge( truthfulness ) ) )
    // Select all expectations linked to $pk
    $obsToPk : Set( ) from collect( Relation(  source isA Observation.class,
                                                        target == $pk,
                                                        type ( == ObservationType.EXPERIMENTATION || == ObservationType.CURATION ) ) )
    $obsSet : Set() from accumulate( $rel: Relation(  ) from $obsToPk,
                                            init  ( Set set =  new HashSet(); ),
                                            action( set.add( $rel.getSource() ); ),
                                            result( set ) )
    $directExpectation: TruthValueSet(  ) from accumulate(   $obs: Observation( ) from $obsSet ,
                                                                init  ( EnumSet set =  EnumSet.noneOf(TruthValue.class);  ),
                                                                action( set.add( $obs.getTruthValue() ); ),
                                                                result( TruthValue.union( set ) ) )

    // Add the direct expectation to merged set of all prediction from parents ( example: {{T}} add {N} give {{T}})
    $result: TruthValuePowerSet( this != $pk.getExpectation(), this == TruthValuePowerSet.add( TruthValuePowerSet.n, $directExpectation ) )

    $pk_children: Set( ) from accumulate(  $rel: Relation(  source isA PriorKnowledge.class, target == $pk ),
                                            init  ( Set set =  new HashSet();  ),
                                            action( set.add(  $rel.getSource() ); ),
                                            result( set )
                                         )
then
    logger.debug( $pk.getName()+" is subtype of one or more parents prior-knowledge and has not the greatest prediction with at least one parent their expectation are not merged " +$expectationFromParents+ ", but we add direct observation " + $directExpectation + " to infer "+$result);
    modify( $pk ){
        setExpectation( $result )
    };
    for( final Object obj : $pk_children){
        final PriorKnowledge child = (PriorKnowledge)obj;
        update(child);
    }
     kcontext.getKnowledgeRuntime().getAgenda().getAgendaGroup("conclusion").setFocus();
     kcontext.getKnowledgeRuntime().getAgenda().getAgendaGroup("prediction inference").setFocus();
end




rule "Prior-knowledge is subtype of an not  an expected prior-knowledge [expectation]" no-loop true agenda-group "expectation inference" when
    // Select a prior-knowledge with a prediction different of {{F}}
    $pk: PriorKnowledge( )
    $relation: Relation(    target  isA PriorKnowledge,
                            source  ==  $pk,
                            type    ==  RelationType.SUBTYPE )
    // take the parent prior-knowledge subtype-of $pk
    $parent: PriorKnowledge( this == $relation.getTarget(), expectation == TruthValuePowerSet.F)
    // Select all expectations linked to $pk
    $obsToPk : Set( ) from collect( Relation(  source isA Observation.class,
                                                        target == $pk,
                                                        type ( == ObservationType.EXPERIMENTATION || == ObservationType.CURATION ) ) )
    $obsSet : Set() from accumulate( $rel: Relation(  ) from $obsToPk,
                                            init  ( Set set =  new HashSet(); ),
                                            action( set.add( $rel.getSource() ); ),
                                            result( set ) )
    $directExpectation: TruthValueSet(  ) from accumulate(   $obs: Observation( ) from $obsSet ,
                                                                init  ( EnumSet set =  EnumSet.noneOf(TruthValue.class);  ),
                                                                action( set.add( $obs.getTruthValue() ); ),
                                                                result( TruthValue.union( set ) ) )
    // Add the direct expectation to parent expectation ( example: {{T}} add {N} give {{T}})
    $result: TruthValuePowerSet( this != $pk.getExpectation(), this == TruthValuePowerSet.add( $parent.getExpectation(), $directExpectation ) )

    $pk_children: Set( ) from accumulate(  $rel: Relation(  source isA PriorKnowledge.class, target == $pk ),
                                            init  ( Set set =  new HashSet();  ),
                                            action( set.add(  $rel.getSource() ); ),
                                            result( set )
                                         )
then
    logger.debug( $pk.getName()+" is subtype of prior-knowledge which is strongly not expected we infer this expectation " +$parent.getExpectation()+ " and we add direct observation " + $directExpectation + " to infer "+$result);
    modify( $pk ){
        setExpectation( $result )
    };
    for( final Object obj : $pk_children){
        final PriorKnowledge child = (PriorKnowledge)obj;
        update(child);
    }
     kcontext.getKnowledgeRuntime().getAgenda().getAgendaGroup("conclusion").setFocus();
     kcontext.getKnowledgeRuntime().getAgenda().getAgendaGroup("prediction inference").setFocus();
end
