/*
* Copyright LABGeM (2014)
*
* author: Jonathan MERCIER
*
* Tis software is a computer program whose purpose is to annotate a complete genome.
*
* Tis software is governed by the CeCILL license under French law and
* abiding by the rules of distribution of free software. You can use,
* modify and/ or redistribute the software under the terms of the CeCILL
* license as circulated by CEA, CNRS and INRIA at the following URL
* "http://www.cecill.info".
*
* As a counterpart to the access to the source code and rights to copy,
* modify and redistribute granted by the license, users are provided only
* with a limited warranty and the software's author, the holder of the
* economic rights, and the successive licensors have only limited
* liability.
*
* In tis respect, the user's attention is drawn to the risks associated
* with loading, using, modifying and/or developing or reproducing the
* software by the user in light of its specific status of free software,
* that may mean that it is complicated to manipulate, and that also
* therefore means that it is reserved for developers and experienced
* professionals having in-depth computer Concept. Users are therefore
* encouraged to load and test the software's suitability as regards their
* requirements in conditions enabling the security of their systems and/or
* data to be ensured and, more generally, to use and operate it in the
* same conditions as regards security.
*
* The fact that you are presently reading tis means that you have had
* Concept of the CeCILL license and that you accept its terms.
*/
package fr.cea.ig.grools;
import fr.cea.ig.grools.fact.Concept;
import fr.cea.ig.grools.fact.Observation;
import fr.cea.ig.grools.fact.ObservationType;
import fr.cea.ig.grools.fact.PriorKnowledge;
import fr.cea.ig.grools.fact.Relation;
import fr.cea.ig.grools.fact.RelationType;
import fr.cea.ig.grools.fact.RelationImpl;
import fr.cea.ig.grools.logic.Math;
import fr.cea.ig.grools.logic.TruthValue;
import fr.cea.ig.grools.logic.TruthValueSet;
import fr.cea.ig.grools.logic.TruthValuePowerSet;

import java.util.Set;
import java.util.HashSet;
import java.util.EnumSet;

global ch.qos.logback.classic.Logger logger;

dialect "java"


rule "Prior-knowledge is part of another prior-knowledge [prediction]" agenda-group "prior-knowledge prediction" when
    $pk: PriorKnowledge()
    $children: Set( size > 0 ) from accumulate(   $rel: Relation(   source isA PriorKnowledge.class,
                                                                    source#PriorKnowledge.getIsDispensable() == false,
                                                                    target == $pk,
                                                                    type == RelationType.PART),
                                                    init  ( Set set =  new HashSet();  ),
                                                    action( set.add( $rel.getSource() ); ),
                                                    result( set )
                                                 )

    $predictionFromChildren: TruthValuePowerSet() from accumulate(  $child: PriorKnowledge( ) from $children,
                                                                    init  ( TruthValuePowerSet truthfulness  =  TruthValuePowerSet.n;  ),
                                                                    action( truthfulness = Math.merge( $child.getPrediction(), truthfulness ); ),
                                                                    result( truthfulness )
                                                                 )
    $result: TruthValuePowerSet( this != $pk.getPrediction(), this ==  Math.merge( $pk.getPrediction(), $predictionFromChildren) )

    $pk_parents: Set( ) from accumulate(   $rel: Relation(  target isA PriorKnowledge.class,
                                                            source == $pk ),
                                            init  ( Set set =  new HashSet();  ),
                                            action( set.add(  $rel.getTarget() ); ),
                                            result( set )
                                         )
then
    modify( $pk ){
        setPrediction($result)
    };
    for( final Object obj : $pk_parents){
        final PriorKnowledge parent = (PriorKnowledge)obj;
        update(parent);
    }
end


rule "Prior-knowledge is part of another prior-knowledge [expectation]" agenda-group "prior-knowledge expectation" when
    $pk: PriorKnowledge( isDispensable == false )
    $parents: TruthValuePowerSet() from accumulate(   $rel: Relation(   target  isA PriorKnowledge.class,
                                                                        source  ==  $pk,
                                                                        type    ==  RelationType.PART ),
                                                    init  ( Set set =  new HashSet();  ),
                                                    action( set.add( $rel.getTarget() ); ),
                                                    result( set )
                                                 )
    $expectationFromParents: TruthValuePowerSet() from accumulate(   $parent: PriorKnowledge( ) from $parents,
                                                                    init  ( TruthValuePowerSet truthfulness  =  TruthValuePowerSet.n;  ),
                                                                    action( truthfulness = Math.merge( $parent.getExpectation(), truthfulness ); ),
                                                                    result( truthfulness )
                                                                 )
    $result: TruthValuePowerSet( this != $pk.getExpectation(), this ==  Math.merge( $pk.getExpectation(), $expectationFromParents) )
    $pk_children: Set( ) from accumulate(  $rel: Relation(  source isA PriorKnowledge.class, target == $pk ),
                                            init  ( Set set =  new HashSet();  ),
                                            action( set.add(  $rel.getSource() ); ),
                                            result( set )
                                         )
then
    modify( $pk ){
        setExpectation($result)
    };
    for( final Object obj : $pk_children){
        final PriorKnowledge child = (PriorKnowledge)obj;
        update(child);
    }
end


rule "Prior-knowledge is subtype of another prior-knowledge [prediction]" agenda-group "prior-knowledge prediction" when
    $pk: PriorKnowledge(  )
    $pk_children: Set( size > 0) from accumulate(   $rel: Relation( source isA PriorKnowledge.class,
                                                                    target == $pk,
                                                                    type == RelationType.SUBTYPE ),
                                                    init  ( Set set =  new HashSet();  ),
                                                    action( set.add( $rel.getSource() ); ),
                                                    result( set )
                                                 )
    $predictionFromChildren: TruthValuePowerSet() from accumulate(  $child: PriorKnowledge( ) from $pk_children,
                                                                    init  ( TruthValuePowerSet set =  TruthValuePowerSet.n;  ),
                                                                    action( set = Math.choice( $child.getPrediction(), set ); ),
                                                                    result( set )
                                                                 )
    $directPrediction: TruthValuePowerSet() from accumulate(   $rel: Relation( source isA Observation.class,
                                                                                target == $pk,
                                                                                type ( == ObservationType.COMPUTATION || == ObservationType.ANNOTATION) ),
                                                                init  ( TruthValueSet set =  TruthValueSet.n;  ),
                                                                action( set = Math.union( ((Observation)$rel.getSource()).getTruthValue() ); ),
                                                                result( TruthValuePowerSet.getByContent( EnumSet.of( set ) ) )
                                                             )
    $result: TruthValuePowerSet( this != $pk.getPrediction(), this ==  Math.merge( $predictionFromChildren, $directPrediction) )

    $pk_parents: Set( ) from accumulate(   $rel: Relation(  target isA PriorKnowledge.class, source == $pk ),
                                            init  ( Set set =  new HashSet();  ),
                                            action( set.add(  $rel.getTarget() );),
                                            result( set )
                                         )
then
    modify( $pk ){
        setPrediction($result)
    };
    for( final Object obj : $pk_parents){
        final PriorKnowledge parent = (PriorKnowledge)obj;
        update(parent);
    }
end


rule "Prior-knowledge is subtype of another prior-knowledge [expectation]" agenda-group "prior-knowledge expectation" when
    $pk: PriorKnowledge()
    $relation: Relation(    target  isA PriorKnowledge.class,
                            source  ==  $pk,
                            type    ==  RelationType.SUBTYPE )
    $parent: PriorKnowledge( this == $relation.getTarget(), expectation != TruthValuePowerSet.F, expectation != $pk.getExpectation() )
    $parent_children: Set( ) from accumulate(  $rel: Relation(  source isA PriorKnowledge.class, target == $parent ),
                                                init  ( Set set =  new HashSet();  ),
                                                action( set.add(  $rel.getSource() ); ),
                                                result( set )
                                             )
    forall( PriorKnowledge( prediction.getTruth() <= $pk.getPrediction().getTruth() ) from $parent_children )
    $result: TruthValuePowerSet( this != $pk.getExpectation(), this ==  Math.merge( $pk.getExpectation(), ((PriorKnowledge)$relation.getTarget()).getExpectation() ) )
    $pk_children: Set( ) from accumulate(  $rel: Relation(  source isA PriorKnowledge.class, target == $pk ),
                                            init  ( Set set =  new HashSet();  ),
                                            action( set.add(  $rel.getSource() ); ),
                                            result( set )
                                         )
then
    modify( $pk ){
        setExpectation($result)
    };
    for( final Object obj : $pk_children){
        final PriorKnowledge child = (PriorKnowledge)obj;
        update(child);
    }
end



rule "Prior-knowledge is subtype of an not expected prior-knowledge [expectation]" agenda-group "prior-knowledge expectation" when
    $pk: PriorKnowledge( expectation != TruthValuePowerSet.F )
    $relation: Relation(    target  isA PriorKnowledge.class,
                            source  ==  $pk,
                            type    ==  RelationType.SUBTYPE )
    $parent: PriorKnowledge( this == $relation.getTarget(), expectation == TruthValuePowerSet.F)

    $pk_children: Set( ) from accumulate(  $rel: Relation(  source isA PriorKnowledge.class, target == $pk ),
                                            init  ( Set set =  new HashSet();  ),
                                            action( set.add(  $rel.getSource() ); ),
                                            result( set )
                                         )
then
    modify( $pk ){
        setExpectation( TruthValuePowerSet.F )
    };
    for( final Object obj : $pk_children){
        final PriorKnowledge child = (PriorKnowledge)obj;
        update(child);
    }
end
